plugins {
    id 'java'
    id 'application'
}

group = 'io.github.tfgcn'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()

    maven { url = 'https://maven.blamejared.com' }
}

dependencies {
    // 命令行工具
    implementation 'info.picocli:picocli:4.7.1'

    // common library
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    implementation 'commons-io:commons-io:2.20.0'
    implementation 'commons-codec:commons-codec:1.19.0'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'com.google.guava:guava:32.1.2-jre'
    implementation 'com.google.code.gson:gson:2.13.1'

    // resolve forge mod dependencies
    implementation 'net.vieiro:toml-java:13.4.2'
    implementation 'org.jgrapht:jgrapht-core:1.5.2'

    implementation 'gov.nist.math:jama:1.0.3'
    implementation 'com.madgag:animated-gif-lib:1.4'
    implementation 'org.freemarker:freemarker:2.3.32'

    // logging
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'ch.qos.logback:logback-classic:1.5.13'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showExceptions = true
        exceptionFormat = "full"
        showCauses = true
        showStackTraces = true
    }

    reports.html.required = false

    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

jar {
    archiveBaseName = 'field-guide-tfg'
    archiveVersion = version

    manifest {
        attributes(
                'Main-Class': 'io.github.tfgcn.fieldguide.Main'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}